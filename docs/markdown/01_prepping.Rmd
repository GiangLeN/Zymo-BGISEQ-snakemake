---
layout: default
title: "Download SRA from NCBI using Snakemake"
author: "Ngoc Giang Le"
version: 0.1
date:
#bibliography:
nav_order: 1
description: "Snakemake tutorial to download SRA"
permalink: /
output:
    html_document:
      toc: true
      toc_float: true
      toc_depth: 3
      fig_caption: true
      highlight: tango
---


```{=html}
<style>
body {
text-align: justify}
</style>
```

## 1. Prepping guides

### 1.1 zymoBIOMICS data on NCBI

The Sequence Read Archive (SRA) contains many sequencing databases from different projects.
We are interested in sequenced zymoBIOMIC Mock community.
Our aim is to use these data to understand more about the sequencing approach and optimize the metagenomic pipeline.
We hope to be able to reproduce the original results and discover new findings from data analysis.


#### 1.1.1 BGISEQ

Name, looks at different extraction methods for BGI sequencing.
Five different extraction methods were tested.

| SampleID   | SRA        | Protocol |
|------------|------------|----------|
| D6300-27   | ERR4097245 | MetaHIT  |
| D6300-28   | ERR4097111 | MetaHIT  |
| D6300-29   | ERR4097243 | MetaHIT  |
| D6300-30   | ERR4097237 | MetaHIT  |
| D6300-31   | ERR4097238 | MetaHIT  |
| D6300-32   | ERR4097276 | MetaHIT  |

The full list of SRA names from this project is located at `sra_files/bgi_sra`.

### 1.2 Conda environments

Conda is great for reproducibility ... as you can install environment with specific tools/programs.


To create an environment with specific name use this basic form:

`conda create --name <environment_name> -c <channel> <tool1> <tool2>`

Use [anaconda web page](https://anaconda.org) to identify the channel for the tool.

To download SRA from ncbi server, we need `sra-tools` from the *bioconda* channel.
With these information we can create a new environment as so.


`conda create --name sra-tools -c bioconda sra-tools`

Activate the environment to use the program.

`conda activate sra-tools`

To return to your previous environment, deactivate `conda`.

`conda deactivate`


<details>
  <summary>Install environment using yaml</summary>

    `conda env create -f <yaml_file_path>`
    
    Yaml files for this project are located at the `envs/` directory.
    
</details>


## 2. Pipeline with Snakemake

Create a new folder for the pipeline.

```
mkdir bgi_download

cd bgi_download
```

### 2.1 Workflow

The workflow/approach needs to be understood before setting up the pipeline with Snakemake.
In this section, we will download *sra* file from NCBI using ... name

Since the file is compressed, we will split it into forward and reverse reads.

Let's download the experimental run ERR4097245 from NCBI by activating the environment we created earlier.

```
conda activate sra-tools

# Download the SRA
prefetch ERR4097245

```

Once completed, we can see a folder called `ERR4097245/` with the file `ERR4097245.sra` inside.

Extract forward and reverse reads from the *sra* file.

```
fasterq-dump --split-files ERR4097245/ERR4097245.sra
```

Two newly generated files ***ERR4097245.sra_1.fastq*** and ***ERR4097245.sra_2.fastq*** correspond to forward and reverse, respectively.

### 2.2 Setup Snakemake


It is easier to reproduce
Everything is treated the same way and so will have fewer errors.

*Mamba* is the preferred way to install Snakemake as it's solver is much faster than *conda* and it can download the latest version.

```
# Install snakemake via mamba
conda create --name snakemake -c conda-forge mamba

# Activate environment
conda activate snakemake

# Use mamba to install snakemake
# This tutorial use snakemake version 7.8.5
mamba install -c conda-forge -c bioconda snakemake=7.8.5

# Check snakemake version
snakemake --version
```

### 2.3 Create rule to download SRA

Open your favorite editor and create a new file called `Snakefile`.

Write the first rule called `download` to download ERR4097245.

```
rule download:
    output:
        "ERR4097245/ERR4097245.sra" 
    shell:
        """
        prefetch -f yes ERR4097245
        """
```

The `output:` tells Snakemake that we are expecting ***ERR4097245/ERR4097245.sra*** as the outcome (same as above).

Running commands/scripts is placed under `shell:`, in this case it's `prefetch <SRA_name>`.
The `-f yes` is to force sra-tools to download the file.

We also need to include rule `all` to specify the *final* outcome.


```
rule all:
    input:
        "ERR4097245/ERR4097245.sra"
```

Save the file and exit out of the editor.

<details>
  <summary>Snakefile</summary>
  
```
rule all:
    input:
        "ERR4097245/ERR4097245.sra"
          
rule download:
    output:
        "ERR4097245/ERR4097245.sra" 
    shell:
        """
        prefetch -f yes ERR4097245
        """
```
            
</details>


Start the pipeline with `snakemake --cores 1`.

```
Building DAG of jobs...
Nothing to be done (all requested files are present and up to date).
```

Since we just downloaded and extract the SRA ERR4097245 in previous step, Snakemake recognizes that the final files are present and so did not run.
Remove the file `ERR4097245/ERR4097245.sra` for rerun.

Let's change the `Snakefile` to download the next SRA sample ERR4097111.


```
rule all:
    input:
        "ERR4097111/ERR4097111.sra"
            
rule download:
    output:
        "ERR4097111/ERR4097111.sra" 
    shell:
        """
        prefetch -f yes ERR4097111
        """
```

![Missing prefetch](img/snakemake_error1.png?raw=true "First error")

Snakemake gives an error as it does not recognise the command *prefetch*.

You might have noticed that we are no longer in the `sra-tools` environment.
This means sra-tools is not installed here.

One option is to install `sra-tool` on to the snakemake environment.
However, we want to maintain a clean base environment and avoid program conflict causes by different package versions.
To solve this, we will tell Snakemake to create new conda environment for the rule instead.


The yaml file contains information of the programs.
You can use the pre-made `envs/sra-tools.yaml` or create one from scratch.

```
name: sra-tools
channels:
 - bioconda
dependencies:
 - sra-tools=2.11.0
```

Update the `download` rule with the line `conda: "envs/sra-tools.yaml"`.


```
rule download:
    output:
        "ERR4097111/ERR4097111.sra" 
    conda:
        "envs/sra-tools.yaml"
    shell:
        """
        prefetch -f yes ERR4097111
        """
```

Save your `Snakefile` and include `--use-conda` in the command:

`snakemake --cores 1 --use-conda`

We are running snakemake locally, and so we only use one core. Snakemake will build the environment the first time it runs.  

![Snakemake conda](img/snakemake_st.png?raw=true "Build conda env for first run")

> Congratulation, you have successfully in using Snakemake to downloaded the first sample.


### 2.4 Extract forward and reverse reads

Same as the second step of the workflow, we want to split the `sra` file into forward and reverse files.
Create a new rule called `split_raw`, which use `fasterq-dump`.


```
rule split_raw:
    input:
        "ERR4097111/ERR4097111.sra"
    output:
        multiext("ERR4097111", "_1.fastq", "_2.fastq")
    conda:
        "envs/sra-tools.yaml"
    shell:
        """
        fasterq-dump --split-files ERR4097111
        """
```

The output from the `download` rule is the input for the new rule `split_raw`. 

The function `multiext()` tells the program that we are expecting files with multiple extensions.

Update rule `all` as we want new output as the final result.



<details>
  <summary>Snakefile</summary>
  
```
rule all:
    input:
        "ERR4097111/ERR4097111.sra",
        multiext("ERR4097111", "_1.fastq", "_2.fastq")

rule download:
    output:
        "ERR4097111/ERR4097111.sra"
    conda:
        "envs/sra-tools.yaml"
    shell:
        """
        prefetch -f yes ERR4097111
        """

rule split_raw:
    input:
        "ERR4097111/ERR4097111.sra"
    output:
        multiext("ERR4097111", "_1.fastq", "_2.fastq")
    conda:
        "envs/sra-tools.yaml"
    shell:
        """
        fasterq-dump --split-files {input}
        """
```
            
</details>


It is possible to remove line `"ERR4097111/ERR4097111.sra"` from rule `all` and does not impact the pipeline at all.
Snakemake looks at the final output and works backward to identify the input required for each rule.
As ***ERR4097111/ERR4097111.sra*** is the input of `split_raw`, Snakemake will trigger rule `download` to create this input.

### 2.5 Compress files

Compress the raw files save valuable space on your hard drive.

Follow these points to make a rule called `compress`.

- Use the output from `split_raw`'s the input.

- The output file type is *gz*

- The command for compressing files is `gzip <file1> <file2>`

- Update rule `all` with the final outcome.


Check here to see the final Snakefile should look like.

<details>
  <summary>Snakefile</summary>
  
```
rule all:
    input:
        multiext("ERR4097111", "_1.fastq.gz", "_2.fastq.gz")

rule download:
    output:
        "ERR4097111/ERR4097111.sra"
    conda:
        "envs/sra-tools.yaml"
    shell:
        """
        prefetch -f yes ERR4097111
        """

rule split_raw:
    input:
        "ERR4097111/ERR4097111.sra"
    output:
        multiext("ERR4097111", "_1.fastq", "_2.fastq")
    conda:
        "envs/sra-tools.yaml"
    shell:
        """
        fasterq-dump --split-files {input}
        """

rule compress:
    input:
        multiext("ERR4097111", "_1.fastq", "_2.fastq")
    output:
        multiext("ERR4097111", "_1.fastq.gz", "_2.fastq.gz")
    shell:
        """
        gzip {input}
        """
       
```
</details>


### 2.6 Update Snakemake to process multiple files

Snakemake is very efficient in processing multiple files.

We need to to rewrite rules using wildcards.

The SRA of interest is provided in a python list.

```
SRA = ["ERR4097111", "ERR4097109"]
```

In the rule, the name of SRA's name is replaced with `{sra}` variable.

```
rule download:
    output:
        temp("{sra}/{sra}.sra") 
    conda:
        "envs/sra-tools.yaml"
    shell:
        """
        prefetch {wildcards.sra} 
        """
```

Use the wrapper `temp()` to tell Snakemake to remove the intermediate output.
The file is removed at the end of the run.


For rule `all`, we want the final output to be `{sra}_1.fastq.gz` and `{sra}_2.fastq.gz`.
Here we need to specify that the value of `{sra}` comes from the SRA list from above.

`expand(["{sra}_1.fastq.gz", "{sra}_2.fastq.gz"], sra = SRA)`

This means, we are looking for 4 files as the final outcome:

-   ERR4097111_1.fastq.gz
-   ERR4097111_2.fastq.gz
-   ERR4097109_1.fastq.gz
-   ERR4097109_2.fastq.gz


To use the variables in the command, we need to specify that we are running `wildcards.sra`.


```
SRA = ["ERR4097111", "ERR4097109"]

rule all:
    input:
        expand(["{sra}_1.fastq.gz", "{sra}_2.fastq.gz"], sra = SRA)


rule download:
    output:
        temp("{sra}/{sra}.sra")
    conda:
        "envs/sra-tools.yaml"
    shell:
        """
        prefetch -f yes {wildcards.sra} 
        """

rule split_raw:
    input:
        "{sra}/{sra}.sra"
    output:
        multiext("{sra}", "_1.fastq", "_2.fastq")
    conda:
        "envs/sra-tools.yaml"
    shell:
        """
        fasterq-dump --split-files {input}
        """

rule compress:
    input:
        multiext("{sra}", "_1.fastq", "_2.fastq")
    output:
        multiext("{sra}", "_1.fastq.gz", "_2.fastq.gz")
    shell:
        """
        gzip {input}
        """
```

Run Snakemake with two cores.
Use the command `-r` to see why the rule was triggered and `-p` to show running commands.

`snakemake --cores 2 --use-conda -r -p`

Snakemake downloads and processes two SRA files at the same time.
Number of parallel process depends on the number of cores used.
To avoid stressing out the NCBI server, reduce the number of cores.
Also, multiple cores will consume more computational power and huge amount of hard disk.

> Congratulation, you used Snakemake to download and process multiple files.

Exercise: Try to download the other SRAs for analysis
